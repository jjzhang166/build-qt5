#*********************************************************************************
#  *Copyright(C): Juntuan.Lu, 2020-2030, All rights reserved.
#  *Author:  Juntuan.Lu
#  *Version: 1.0
#  *Date:  2021/11/29
#  *Email: 931852884@qq.com
#  *Description:
#  *Others:
#  *Function List:
#  *History:
#**********************************************************************************

MAKEFILE_GENERATOR              = UNIX
QMAKE_PLATFORM                  = android
QMAKE_COMPILER                  = gcc clang llvm

NDK_LLVM_PATH                   = $$(NDK_LLVM_PATH)
isEmpty(NDK_LLVM_PATH): error("You need to set the NDK_LLVM_PATH environment.")

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/clang.conf)

load(device_config)

ANDROID_PLATFORM                = android-29
ANDROID_ABI                     = arm64-v8a
EGLFS_DEVICE_INTEGRATION        = eglfs_sffg
ANDROID_USE_LLVM                = true
ALL_ANDROID_ABIS                = $$ANDROID_ABI
arm64-v8a.sdk                   = $$ANDROID_ABI
arm64-v8a.target                = $$ANDROID_ABI
arm64-v8a.dir_affix             = $$ANDROID_ABI
arm64-v8a.CONFIG                = $$ANDROID_ABI
arm64-v8a.deployment_identifier = $$ANDROID_ABI
CONFIG                          += $$ANDROID_PLATFORM android-embedded
DEFINES                         += ANDROID_EMBEDDED

QMAKE_CC                        = $$NDK_LLVM_PATH/bin/clang
QMAKE_CXX                       = $$NDK_LLVM_PATH/bin/clang++
QMAKE_AR                        = $$NDK_LLVM_PATH/bin/llvm-ar cqs
QMAKE_RANLIB                    = $$NDK_LLVM_PATH/bin/llvm-ranlib
QMAKE_OBJCOPY                   = $$NDK_LLVM_PATH/bin/llvm-objcopy
QMAKE_OBJDUMP                   = $$NDK_LLVM_PATH/bin/llvm-objdump
QMAKE_NM                        = $$NDK_LLVM_PATH/bin/llvm-nm -P
QMAKE_AS                        = $$NDK_LLVM_PATH/bin/llvm-as
QMAKE_STRIP                     = $$NDK_LLVM_PATH/bin/llvm-strip

QMAKE_LINK_C                    = $$QMAKE_CC
QMAKE_LINK_C_SHLIB              = $$QMAKE_CC
QMAKE_LINK                      = $$QMAKE_CXX
QMAKE_LINK_SHLIB                = $$QMAKE_CXX

QMAKE_LIBS_X11                  =
QMAKE_LIBS_THREAD               =
QMAKE_LIBS_OPENGL               =
QMAKE_INCDIR_POST               =
QMAKE_INCDIR_X11                =
QMAKE_LIBDIR_X11                =
QMAKE_INCDIR_OPENGL             =
QMAKE_LIBDIR_OPENGL             =

BUILD_TARGET_PREFIX             = $$(BUILD_TARGET_PREFIX)
TARGET_SYSROOT                  = $$(TARGET_SYSROOT)
!isEmpty(BUILD_TARGET_PREFIX) {
    QMAKE_DEFAULT_INCDIRS += $$BUILD_TARGET_PREFIX/include
    QMAKE_DEFAULT_LIBDIRS += $$BUILD_TARGET_PREFIX/lib
    QMAKE_INCDIR += $$BUILD_TARGET_PREFIX/include
    QMAKE_LIBDIR += $$BUILD_TARGET_PREFIX/lib
} else {
    !isEmpty(TARGET_SYSROOT) {
        QMAKE_DEFAULT_INCDIRS += $$TARGET_SYSROOT/usr/include
        QMAKE_DEFAULT_LIBDIRS += $$TARGET_SYSROOT/usr/lib
        QMAKE_INCDIR += $$TARGET_SYSROOT/usr/include
        QMAKE_LIBDIR += $$TARGET_SYSROOT/usr/lib
    }
}

load(qt_config)
